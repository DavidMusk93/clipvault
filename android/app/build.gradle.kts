import java.util.Properties

plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
    id("org.jetbrains.kotlin.plugin.serialization")
}

val localProps = Properties().apply {
    val f = rootProject.file("local.properties")
    if (f.exists()) f.inputStream().use { load(it) }
}

fun prop(name: String, default: String = ""): String =
    System.getenv(name)
        ?: (project.findProperty(name) as? String)
        ?: localProps.getProperty(name)
        ?: default

android {
    namespace = "com.davidmusk.clipvault"
    compileSdk = 34

    defaultConfig {
        applicationId = "com.davidmusk.clipvault"
        minSdk = 26
        targetSdk = 34
        versionCode = (System.getenv("CLIPVAULT_VERSION_CODE") ?: System.getenv("KEEPSAKE_VERSION_CODE") ?: "2").toInt()
        versionName = System.getenv("CLIPVAULT_VERSION_NAME") ?: System.getenv("KEEPSAKE_VERSION_NAME") ?: "0.1.1"
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        create("ci") {
            val store = rootProject.file("keystore/keepsake-ci.jks")
            if (store.exists()) {
                storeFile = store
                storePassword = prop("CLIPVAULT_STORE_PASSWORD", "keepsake-ci")
                keyAlias = prop("CLIPVAULT_KEY_ALIAS", "keepsake")
                keyPassword = prop("CLIPVAULT_KEY_PASSWORD", "keepsake-ci")
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix = ".debug"
            versionNameSuffix = "-debug"
        }
        release {
            isMinifyEnabled = false
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
            val ci = signingConfigs.findByName("ci")
            if (ci?.storeFile?.exists() == true) {
                signingConfig = ci
            }
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }
    buildFeatures {
        compose = true
        buildConfig = true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.14"
    }
    packaging {
        resources.excludes += "/META-INF/{AL2.0,LGPL2.1}"
    }
}

dependencies {
    val composeBom = platform("androidx.compose:compose-bom:2024.06.00")
    implementation(composeBom)
    androidTestImplementation(composeBom)

    implementation("androidx.core:core-ktx:1.13.1")
    implementation("androidx.activity:activity-compose:1.9.0")
    implementation("androidx.lifecycle:lifecycle-runtime-ktx:2.8.3")
    implementation("androidx.lifecycle:lifecycle-viewmodel-compose:2.8.3")
    implementation("androidx.lifecycle:lifecycle-runtime-compose:2.8.3")
    implementation("androidx.navigation:navigation-compose:2.7.7")
    implementation("androidx.compose.ui:ui")
    implementation("androidx.compose.ui:ui-tooling-preview")
    implementation("androidx.compose.material3:material3")
    implementation("androidx.compose.material:material-icons-extended")
    implementation("androidx.documentfile:documentfile:1.0.1")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.1")
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.3")
    implementation("androidx.work:work-runtime-ktx:2.9.1")

    debugImplementation("androidx.compose.ui:ui-tooling")
    debugImplementation("androidx.compose.ui:ui-test-manifest")

    testImplementation("junit:junit:4.13.2")
}
